{-
  TASK 2 - Matrix multiplier
  Write the method that multiplies one matrix by another.
  The 1st and 2nd parameter of the function will contain
  a matrix implemented using tuples (eg: [[1,2], [3,4]]).
  Matrices can be of different sizes, ranging from 4x4
  up to 32x32.
  Matrix can be rectangular where matrixA must to be of size n*m & matrixB of size m*p.
  After calculating the result matrix, the function should return final result (as tuple).
-}

() recv_internal() {
}

(int) tlen (tuple t) asm "TLEN";



;; testable
(tuple) matrix_multiplier(tuple matrixA, tuple matrixB) method_id {
  int n = matrixA.tlen();
  int p = matrixB.first().tlen();
  int m = matrixA.first().tlen();

  tuple matrixC = empty_tuple();
  int i = 0;
  while (i < n) {
    int j = 0;
    tuple newRow = empty_tuple();
    while (j < p) {
      int k = 0;
      int newEntry = 0;
      while (k < m) {
        newEntry += matrixA.at(i).at(k) * matrixB.at(k).at(j);
        k += 1;
      }
      newRow~tpush(newEntry);
      j += 1;
    }
    matrixC~tpush(newRow);
    i += 1;
  }
  
  return matrixC;
}
